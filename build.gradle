import org.maciejszczurek.ChromeDriverPlugin

plugins {
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.github.spotbugs" version '6.0.27'
    id "org.openjfx.javafxplugin" version "0.1.0"
    id "com.google.osdetector" version "1.7.3"
    id "com.google.protobuf" version "0.9.4"
    id 'java'
    id 'pmd'
}

apply plugin: ChromeDriverPlugin

group = 'com.maciejszczurek'
version = '41.3.40'
sourceCompatibility = JavaVersion.VERSION_23

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://projectlombok.org/edge-releases' }
}

bootJar {
    archiveFileName = "${archiveBaseName.get()}-${project.version}-${osdetector.os}.jar"
    mainClass = "com.maciejszczurek.updatechecker.UpdateCheckerApplication"
}

downloadChromeDriver {
    version = '131.0.6778.204'
}

javafx {
    version = JavaVersion.VERSION_23
    modules = ['javafx.controls', 'javafx.fxml']
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.28.3'
    }
}

pmd {
    consoleOutput = true
    toolVersion = '7.8.0'
    ruleSets = ['category/java/bestpractices.xml',
                'category/java/codestyle.xml',
                'category/java/design.xml',
                'category/java/errorprone.xml',
                'category/java/multithreading.xml',
                'category/java/performance.xml']
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "com.google.protobuf:protobuf-java:4.28.3"
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    implementation 'commons-io:commons-io:2.18.0'
    implementation "org.hibernate.orm:hibernate-core:6.6.4.Final"
    implementation "org.hibernate.validator:hibernate-validator:8.0.1.Final"
    implementation 'net.java.dev.jna:jna:5.16.0'
    implementation "org.seleniumhq.selenium:selenium-api:4.27.0"
    implementation "org.seleniumhq.selenium:selenium-http:4.27.0"
    implementation "org.seleniumhq.selenium:selenium-json:4.27.0"
    implementation "org.seleniumhq.selenium:selenium-remote-driver:4.27.0"
    implementation "org.seleniumhq.selenium:selenium-chrome-driver:4.27.0"
    implementation "org.seleniumhq.selenium:selenium-chromium-driver:4.27.0"
    implementation "org.seleniumhq.selenium:selenium-support:4.27.0"
    implementation "org.seleniumhq.selenium:selenium-manager:4.27.0"
    implementation "org.seleniumhq.selenium:selenium-java:4.27.0"
    implementation "com.github.mouse0w0:darculafx:9.0.0"
//    implementation "org.jfxtras:jmetro:11.6.15"
    implementation "org.jsoup:jsoup:1.18.3"
    implementation 'org.jetbrains:annotations:26.0.1'
    implementation "org.apache.commons:commons-lang3:3.17.0"
    implementation "org.apache.commons:commons-pool2:2.12.0"
    implementation 'org.apache.commons:commons-compress:1.27.1'
    implementation "com.pivovarit:throwing-function:1.6.1"
    implementation 'org.projectlombok:lombok:1.18.36'
    runtimeOnly 'com.h2database:h2:2.3.232'
    compileOnly "org.projectlombok:lombok:1.18.36"
    annotationProcessor "org.projectlombok:lombok:1.18.36"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0"
}

test {
    useJUnitPlatform()
}

tasks.register('compileAndMove') {
    dependsOn bootJar
    doLast {
        ant.move file: "${projectDir}/build/libs/${bootJar.archiveFileName.get()}",
                tofile: "${moveDir}/update-checker.jar"
    }
}
