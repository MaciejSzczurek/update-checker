import com.maciejszczurek.ChromeDriverPlugin

plugins {
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id "com.github.spotbugs" version "5.0.13"
    id "org.openjfx.javafxplugin" version "0.0.13"
    id 'java'
    id 'pmd'
}

apply plugin: ChromeDriverPlugin

group = 'com.maciejszczurek'
version = '39.0.10'
sourceCompatibility = JavaVersion.VERSION_19

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://projectlombok.org/edge-releases' }
}

downloadChromeDriver {
    version = '108.0.5359.71'
}

javafx {
    version = JavaVersion.VERSION_19
    modules = ['javafx.controls', 'javafx.fxml']
}

sourceSets {
    main {
        resources {
            srcDirs += ['libs/chromedriver']
        }
    }
}

pmd {
    consoleOutput = true
    toolVersion = '6.53.0'
    ruleSets = ['category/java/bestpractices.xml',
                'category/java/codestyle.xml',
                'category/java/design.xml',
                'category/java/errorprone.xml',
                'category/java/multithreading.xml',
                'category/java/performance.xml']
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "com.google.protobuf:protobuf-java:3.21.12"
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
    implementation 'commons-io:commons-io:2.11.0'
    implementation "org.hibernate.orm:hibernate-core:6.1.6.Final"
    implementation "org.hibernate.validator:hibernate-validator:7.0.5.Final"
    implementation 'net.java.dev.jna:jna:5.12.1'
    implementation "org.seleniumhq.selenium:selenium-api:4.7.2"
    implementation "org.seleniumhq.selenium:selenium-remote-driver:4.7.2"
    implementation "org.seleniumhq.selenium:selenium-chrome-driver:4.7.2"
    implementation "org.seleniumhq.selenium:selenium-support:4.7.2"
    implementation "com.github.mouse0w0:darculafx:9.0.0"
//    implementation "org.jfxtras:jmetro:11.6.15"
    implementation "org.jsoup:jsoup:1.15.3"
    implementation "org.jetbrains:annotations:23.1.0"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "org.apache.commons:commons-pool2:2.11.1"
    implementation "com.pivovarit:throwing-function:1.5.1"
    implementation 'org.projectlombok:lombok:1.18.24'
    runtimeOnly 'com.h2database:h2:2.1.214'
//    compileOnly "org.projectlombok:lombok:1.18.24"
    compileOnly files("libs/lombok-jdk19-20221010.jar")
//    annotationProcessor "org.projectlombok:lombok:1.18.24"
    annotationProcessor files("libs/lombok-jdk19-20221010.jar")
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0"
}

test {
    useJUnitPlatform()
}

task compileAndMove {
    dependsOn bootJar
    doLast {
        ant.move file: "${buildDir}/libs/update-checker-${version}.jar",
                tofile: "${moveDir}/update-checker.jar"
    }
}
