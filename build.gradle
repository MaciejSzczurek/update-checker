import org.maciejszczurek.ChromeDriverPlugin

plugins {
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id "com.github.spotbugs" version '6.0.9'
    id "org.openjfx.javafxplugin" version "0.1.0"
    id "com.google.osdetector" version "1.7.3"
    id "com.google.protobuf" version "0.9.4"
    id 'java'
    id 'pmd'
}

apply plugin: ChromeDriverPlugin

group = 'com.maciejszczurek'
version = '41.2.14'
sourceCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://projectlombok.org/edge-releases' }
}

bootJar {
    archiveFileName = "${archiveBaseName.get()}-${project.version}-${osdetector.os}.jar"
}

downloadChromeDriver {
    version = '123.0.6312.105'
}

javafx {
    version = JavaVersion.VERSION_21
    modules = ['javafx.controls', 'javafx.fxml']
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.26.0'
    }
}

pmd {
    consoleOutput = true
    toolVersion = '6.55.0'
    ruleSets = ['category/java/bestpractices.xml',
                'category/java/codestyle.xml',
                'category/java/design.xml',
                'category/java/errorprone.xml',
                'category/java/multithreading.xml',
                'category/java/performance.xml']
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "com.google.protobuf:protobuf-java:4.26.0"
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'commons-io:commons-io:2.16.1'
    implementation "org.hibernate.orm:hibernate-core:6.4.4.Final"
    implementation "org.hibernate.validator:hibernate-validator:8.0.1.Final"
    implementation 'net.java.dev.jna:jna:5.14.0'
    implementation "org.seleniumhq.selenium:selenium-api:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-http:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-json:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-remote-driver:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-chrome-driver:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-chromium-driver:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-support:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-manager:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-java:4.19.1"
    implementation "org.seleniumhq.selenium:selenium-http-jdk-client:4.13.0"
    implementation "com.github.mouse0w0:darculafx:9.0.0"
//    implementation "org.jfxtras:jmetro:11.6.15"
    implementation "org.jsoup:jsoup:1.17.2"
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation "org.apache.commons:commons-lang3:3.14.0"
    implementation "org.apache.commons:commons-pool2:2.12.0"
    implementation 'org.apache.commons:commons-compress:1.26.1'
    implementation "com.pivovarit:throwing-function:1.5.1"
    implementation 'org.projectlombok:lombok:1.18.32'
    runtimeOnly 'com.h2database:h2:2.2.224'
    compileOnly "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.projectlombok:lombok:1.18.32"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0"
}

test {
    useJUnitPlatform()
}

tasks.register('compileAndMove') {
    dependsOn bootJar
    doLast {
        ant.move file: "${projectDir}/build/libs/${bootJar.archiveFileName.get()}",
                tofile: "${moveDir}/update-checker.jar"
    }
}
